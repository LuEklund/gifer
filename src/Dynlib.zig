const DynLib = @This();

const std = @import("std");
const builtin = @import("builtin");

const InnerType = switch (builtin.os.tag) {
    .windows, .wasi => WindowsDynLib,
    else => std.DynLib,
};

inner: InnerType,

pub fn open(path: []const u8) !DynLib {
    return .{ .inner = try InnerType.open(path) };
}

pub fn openZ(path: [*:0]const u8) !DynLib {
    return .{ .inner = try InnerType.openZ(path) };
}

pub fn close(self: *DynLib) void {
    self.inner.close();
}

pub fn lookup(self: *DynLib, comptime T: type, name: [:0]const u8) ?T {
    return self.inner.lookup(T, name);
}

const WindowsDynLib = struct {
    handle: std.os.windows.HMODULE,

    extern "kernel32" fn LoadLibraryW(path: [*:0]const u16) callconv(.winapi) ?std.os.windows.HMODULE;
    extern "kernel32" fn FreeLibrary(module: std.os.windows.HMODULE) callconv(.winapi) std.os.windows.BOOL;
    extern "kernel32" fn GetProcAddress(module: std.os.windows.HMODULE, name: [*:0]const u8) callconv(.winapi) ?*anyopaque;

    fn open(path: []const u8) !WindowsDynLib {
        var buf: [std.fs.max_path_bytes]u16 = undefined;
        const len = try std.unicode.utf8ToUtf16Le(buf[0 .. buf.len - 1], path);
        buf[len] = 0;
        const handle = LoadLibraryW(buf[0..len :0].ptr) orelse return error.FileNotFound;
        return .{ .handle = handle };
    }

    fn openZ(path: [*:0]const u8) !WindowsDynLib {
        return .open(std.mem.span(path));
    }

    fn close(self: *WindowsDynLib) void {
        _ = FreeLibrary(self.handle);
        self.* = undefined;
    }

    fn lookup(self: *WindowsDynLib, comptime T: type, name: [:0]const u8) ?T {
        return @ptrCast(GetProcAddress(self.handle, name.ptr) orelse return null);
    }
};
