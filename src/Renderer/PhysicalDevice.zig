const PhysicalDevice = @This();

const std = @import("std");
const vk = @import("vulkan");

const Instance = @import("Instance.zig");
const Surface = @import("Surface.zig");

handle: vk.PhysicalDevice,
properties: vk.PhysicalDeviceProperties,
memory_properties: vk.PhysicalDeviceMemoryProperties,
graphics_queue_family_index: u32,
sampler_descriptor_size: vk.DeviceSize,

pub const PickError =
    vk.InstanceWrapper.EnumeratePhysicalDevicesAllocError ||
    vk.InstanceWrapper.GetPhysicalDeviceSurfaceSupportKHRError ||
    std.mem.Allocator.Error ||
    error{ NoDevices, MissingSuitableDevice };

pub fn pick(gpa: std.mem.Allocator, instance: Instance, surface: Surface) PickError!PhysicalDevice {
    const physical_devices = try instance.proxy.enumeratePhysicalDevicesAlloc(gpa);
    defer gpa.free(physical_devices);

    if (physical_devices.len == 0) return error.NoDevices;

    var best: ?PhysicalDevice = null;
    var best_score: i32 = -1;

    var physical_device_descriptor_buffer_properties: vk.PhysicalDeviceDescriptorBufferPropertiesEXT = undefined;
    physical_device_descriptor_buffer_properties.s_type = .physical_device_descriptor_buffer_properties_ext;
    physical_device_descriptor_buffer_properties.p_next = null;
    var physical_device_properties: vk.PhysicalDeviceProperties2 = .{
        .p_next = &physical_device_descriptor_buffer_properties,
        .properties = undefined,
    };
    for (physical_devices) |physical_device| {
        instance.proxy.getPhysicalDeviceProperties2(
            physical_device,
            &physical_device_properties,
        );
        const properties = physical_device_properties.properties;
        const memory_properties = instance.proxy.getPhysicalDeviceMemoryProperties(physical_device);

        const families = try instance.proxy.getPhysicalDeviceQueueFamilyPropertiesAlloc(physical_device, gpa);
        defer gpa.free(families);

        const queue_family = for (families, 0..) |family, i| {
            const index: u32 = @truncate(i);

            const graphics_support = family.queue_flags.graphics_bit;
            const surface_support = try instance.proxy.getPhysicalDeviceSurfaceSupportKHR(physical_device, index, surface.handle) == .true;

            if (graphics_support and surface_support) break index;
        } else continue;

        var score: i32 = switch (properties.device_type) {
            .discrete_gpu => 1000,
            .integrated_gpu => 100,
            .virtual_gpu => 67,
            .cpu => 10,
            else => 0,
        };

        score += @as(i32, @intCast(properties.limits.max_image_dimension_2d / 1024));

        if (score > best_score) {
            best_score = score;
            best = .{
                .handle = physical_device,
                .properties = properties,
                .memory_properties = memory_properties,
                .graphics_queue_family_index = queue_family,
                .sampler_descriptor_size = physical_device_descriptor_buffer_properties.combined_image_sampler_descriptor_size,
            };
        }
    }

    if (best) |physical_device| {
        std.log.info("selected ({t}) {s}", .{ physical_device.properties.device_type, physical_device.properties.device_name });
        return physical_device;
    }

    return error.MissingSuitableDevice;
}

pub fn findMemoryType(type_filter: u32, properties: vk.MemoryPropertyFlags, memory_properties: vk.PhysicalDeviceMemoryProperties) u32 {
    for (0..memory_properties.memory_type_count) |i| {
        const supported = (type_filter & (@as(u32, 1) << @intCast(i))) != 0;
        const flags = memory_properties.memory_types[i].property_flags;

        if (supported and flags.contains(properties)) {
            return @intCast(i);
        }
    }

    unreachable;
}
